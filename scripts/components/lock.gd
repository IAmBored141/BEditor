extends GameComponent
class_name Lock

const TYPES:int = 7
enum TYPE {NORMAL, BLANK, BLAST, ALL, EXACT, GLISTENING, REMAINDER}
const TYPE_NAMES:Array[String] = ["Normal", "Blank", "Blast", "All", "Exact", "Glistening", "Remainder"]
const SIZE_TYPES:int = 7
enum SIZE_TYPE {AnyS, AnyH, AnyV, AnyM, AnyL, AnyXL, ANY}
const SIZE_TYPE_NAMES:Array[String] = ["AnyS", "AnyH", "AnyV", "AnyM", "AnyL", "AnyXL", "ANY"]
const SIZES:Array[Vector2] = [Vector2(18,18), Vector2(50,18), Vector2(18,50), Vector2(38,38), Vector2(50,50), Vector2(82,82)]
enum CONFIGURATION {spr1A, spr2H, spr2V, spr3H, spr3V, spr4A, spr4B, spr5A, spr5B, spr6A, spr6B, spr8A, spr12A, spr24A, spr7A, spr9A, spr9B, spr10A, spr11A, spr13A, spr24B, NONE}
const CONFIGURATION_NAMES:Array[String] = ["1A", "2H", "2V", "3H", "3V", "4A", "4B", "5A", "5B", "6A", "6B", "8A", "12A", "24A", "7A", "9A", "9B", "10A", "11A", "13A", "24B"]
const PREDEFINED_REAL:Array[CONFIGURATION] = [CONFIGURATION.spr1A, CONFIGURATION.spr2H, CONFIGURATION.spr2V, CONFIGURATION.spr3H, CONFIGURATION.spr3V, CONFIGURATION.spr4A, CONFIGURATION.spr4B, CONFIGURATION.spr5A, CONFIGURATION.spr5B, CONFIGURATION.spr6A, CONFIGURATION.spr6B, CONFIGURATION.spr8A, CONFIGURATION.spr12A, CONFIGURATION.spr24A, CONFIGURATION.spr7A, CONFIGURATION.spr9A, CONFIGURATION.spr9B, CONFIGURATION.spr10A, CONFIGURATION.spr11A, CONFIGURATION.spr13A, CONFIGURATION.spr24B]
const PREDEFINED_IMAGINARY:Array[CONFIGURATION] = [CONFIGURATION.spr1A, CONFIGURATION.spr2H, CONFIGURATION.spr2V, CONFIGURATION.spr3H, CONFIGURATION.spr3V]

const SPEND_TYPES = 4
enum SPEND_TYPE {NONE, NORMAL, STAR, ALL}

func getAvailableConfigurations() -> Array[Array]: return availableConfigurations(effectiveCount(), type)

static func availableConfigurations(lockCount:PackedInt64Array, lockType:TYPE) -> Array[Array]:
	# returns Array[Array[SIZE_TYPE, CONFIGURATION]]
	# SpecificA/H first, then SpecificB/V
	var available:Array[Array] = []
	if lockType != TYPE.NORMAL and lockType != TYPE.EXACT: return available
	var absCount:PackedInt64Array = M.cabs(lockCount)
	if M.isNonzeroReal(lockCount):
		if M.eq(absCount, M.ONE()): available.append([SIZE_TYPE.AnyS, CONFIGURATION.spr1A])
		elif M.eq(absCount, M.N(2)): available.append([SIZE_TYPE.AnyH, CONFIGURATION.spr2H]); available.append([SIZE_TYPE.AnyV, CONFIGURATION.spr2V])
		elif M.eq(absCount, M.N(3)): available.append([SIZE_TYPE.AnyH, CONFIGURATION.spr3H]); available.append([SIZE_TYPE.AnyV, CONFIGURATION.spr3V])
		elif M.eq(absCount, M.N(4)): available.append([SIZE_TYPE.AnyM, CONFIGURATION.spr4A]); available.append([SIZE_TYPE.AnyL, CONFIGURATION.spr4B])
		elif M.eq(absCount, M.N(5)): available.append([SIZE_TYPE.AnyM, CONFIGURATION.spr5A]); available.append([SIZE_TYPE.AnyL, CONFIGURATION.spr5B])
		elif M.eq(absCount, M.N(6)): available.append([SIZE_TYPE.AnyM, CONFIGURATION.spr6A]); available.append([SIZE_TYPE.AnyL, CONFIGURATION.spr6B])
		elif M.eq(absCount, M.N(8)): available.append([SIZE_TYPE.AnyL, CONFIGURATION.spr8A])
		elif M.eq(absCount, M.N(12)): available.append([SIZE_TYPE.AnyL, CONFIGURATION.spr12A])
		elif M.eq(absCount, M.N(24)):
			available.append([SIZE_TYPE.AnyXL, CONFIGURATION.spr24A])
			if Mods.active("MoreLockConfigs"): available.append([SIZE_TYPE.AnyXL, CONFIGURATION.spr24B])
		elif Mods.active("MoreLockConfigs"):
			if M.eq(absCount, M.N(7)): available.append([SIZE_TYPE.AnyL, CONFIGURATION.spr7A])
			elif M.eq(absCount, M.N(9)): available.append([SIZE_TYPE.AnyL, CONFIGURATION.spr9A]); available.append([SIZE_TYPE.AnyL, CONFIGURATION.spr9B])
			elif M.eq(absCount, M.N(10)): available.append([SIZE_TYPE.AnyL, CONFIGURATION.spr10A])
			elif M.eq(absCount, M.N(11)): available.append([SIZE_TYPE.AnyL, CONFIGURATION.spr11A])
			elif M.eq(absCount, M.N(13)): available.append([SIZE_TYPE.AnyL, CONFIGURATION.spr13A])
	elif M.isNonzeroImag(lockCount):
		if M.eq(absCount, M.I()): available.append([SIZE_TYPE.AnyS, CONFIGURATION.spr1A])
		elif M.eq(absCount, M.Ni(2)): available.append([SIZE_TYPE.AnyH, CONFIGURATION.spr2H]); available.append([SIZE_TYPE.AnyV, CONFIGURATION.spr2V])
		elif M.eq(absCount, M.Ni(3)): available.append([SIZE_TYPE.AnyH, CONFIGURATION.spr3H]); available.append([SIZE_TYPE.AnyV, CONFIGURATION.spr3V])
	return available

const ANY_RECT:Rect2 = Rect2(Vector2.ZERO,Vector2(50,50)) # rect of ANY
const CORNER_SIZE:Vector2 = Vector2(2,2) # size of ANY's corners
const GLITCH_ANY_RECT:Rect2 = Rect2(Vector2.ZERO,Vector2(82,82))
const GLITCH_CORNER_SIZE:Vector2 = Vector2(9,9)
const TILE:RenderingServer.NinePatchAxisMode = RenderingServer.NinePatchAxisMode.NINE_PATCH_TILE # just to save characters
const STRETCH:RenderingServer.NinePatchAxisMode = RenderingServer.NinePatchAxisMode.NINE_PATCH_STRETCH # just to save characters

static var PREDEFINED_SPRITE_REAL:LockPredefinedTextureLoader = LockPredefinedTextureLoader.new("res://assets/game/lock/predefined/$p.png", PREDEFINED_REAL)
static var PREDEFINED_SPRITE_IMAGINARY:LockPredefinedTextureLoader = LockPredefinedTextureLoader.new("res://assets/game/lock/predefined/$pi.png", PREDEFINED_IMAGINARY)

static func getPredefinedLockSprite(lockCount:PackedInt64Array, lockType:TYPE, lockConfiguration:CONFIGURATION) -> Texture2D:
	if M.isNonzeroImag(lockCount): return PREDEFINED_SPRITE_IMAGINARY.current([lockType==TYPE.EXACT,lockConfiguration])
	else: return PREDEFINED_SPRITE_REAL.current([lockType==TYPE.EXACT,lockConfiguration])

const FRAME_HIGH:Texture2D = preload("res://assets/game/lock/frame/high.png")
const FRAME_MAIN:Texture2D = preload("res://assets/game/lock/frame/main.png")
const FRAME_DARK:Texture2D = preload("res://assets/game/lock/frame/dark.png")

static func getFrameHighColor(_isNegative:bool, _negated:bool) -> Color:
	if _isNegative: return Color("#14202c") if _negated else Color("#ebdfd3")
	else: return Color("#7b9fc3") if _negated else Color("#84603c")

static func getFrameMainColor(_isNegative:bool, _negated:bool) -> Color:
	if _isNegative: return Color("#274058") if _negated else Color("#d8bfa7")
	else: return Color("#a7bfd8") if _negated else Color("#584027")

static func getFrameDarkColor(_isNegative:bool, _negated:bool) -> Color:
	if _isNegative: return Color("#3b6084") if _negated else Color("#c49f7b")
	else: return Color("#d3dfeb") if _negated else Color("#42301d")

const SYMBOL_NORMAL = preload("res://assets/game/lock/symbols/normal.png")
const SYMBOL_BLAST = preload("res://assets/game/lock/symbols/blast.png")
const SYMBOL_BLASTI = preload("res://assets/game/lock/symbols/blasti.png")
const SYMBOL_EXACT = preload("res://assets/game/lock/symbols/exact.png")
const SYMBOL_EXACTI = preload("res://assets/game/lock/symbols/exacti.png")
const SYMBOL_ALL = preload("res://assets/game/lock/symbols/all.png")
const SYMBOL_GLISTENING = preload("res://assets/game/lock/symbols/glistening.png")
const SYMBOL_GLISTENINGI = preload("res://assets/game/lock/symbols/glisteningi.png")
const SYMBOL_REMAINDER = preload("res://assets/game/lock/symbols/remainder.png")
const SYMBOL_SIZE:Vector2 = Vector2(32,32)

const PARTIAL_BLAST_HORIZONTAL = preload("res://assets/game/lock/symbols/partialBlastHorizontal.png")
const PARTIAL_BLAST_HORIZONTAL_BIG = preload("res://assets/game/lock/symbols/partialBlastHorizontalBig.png")

static var GLITCH_FILL:LockTextureLoader = LockTextureLoader.new("res://assets/game/lock/fill/$tglitch.png")
static var GLITCH_FILL_TEXTURE:LockColorsTextureLoader = LockColorsTextureLoader.new("res://assets/game/lock/fill/$tglitch$c.png",false,true)

static var ERROR_FX:IndexTextureLoader = IndexTextureLoader.new("res://assets/game/key/error/fx.png", 3)

const ARMAMENT:Array[Texture2D] = [
	preload("res://assets/game/lock/armament/0.png"),
	preload("res://assets/game/lock/armament/1.png"),
	preload("res://assets/game/lock/armament/2.png"),
	preload("res://assets/game/lock/armament/3.png")
]
const ARMAMENT_RECT:Rect2 = Rect2(Vector2.ZERO, Vector2(18,18))
const ARMAMENT_CORNER_SIZE:Vector2 = Vector2(5,5)

const BACKGROUND_STARRY = preload("res://assets/game/lock/background/starry.png")
const BACKGROUND_WEAK = preload("res://assets/game/lock/background/weak.png")
const BACKGROUND_FORCEFUL = preload("res://assets/game/lock/background/forceful.png")

static func offsetFromType(getSizeType:SIZE_TYPE) -> Vector2:
	match getSizeType:
		SIZE_TYPE.AnyM: return Vector2(3, 3)
		_: return Vector2(-7, -7)

func getOffset() -> Vector2: return offsetFromType(sizeType)

const CREATE_PARAMETERS:Array[StringName] = [
	&"position", &"parentId"
]

var parent:Door
@export_group("SavedProperties")
@export var parentId:int
@export var color:C.olors = C.olors.WHITE
@export var type:TYPE = TYPE.NORMAL
@export var configuration:CONFIGURATION = CONFIGURATION.spr1A
@export var sizeType:SIZE_TYPE = SIZE_TYPE.AnyS
@export var count:PackedInt64Array = M.ONE()
@export var zeroI:bool = false # if the count is zeroI, for exact locks
@export var isPartial:bool = false # for partial blast
@export var denominator:PackedInt64Array = M.ONE() # for partial blast
@export var partialBlastHorizontal:bool = false # for partial blast
@export var negated:bool = false
@export var armament:bool = false
@export var index:int # implicit
@export var displayIndex:int # implicit, split into armaments and nonarmaments
# in my mind, two buttons to toggle "spends star" and "spends normal" is more intuitive than 4 buttons which toggle
@export var spendType = SPEND_TYPE.NORMAL

func getColors() -> Array[C.olors]: return [color]

var drawScaled:RID
var drawAuraBreaker:RID
var drawGlitch:RID
var drawMain:RID
var drawError:RID
var drawConfiguration:RID
var textDrawer:TextDrawer

static func getConfigurationColor(_isNegative:bool) -> Color:
	if _isNegative: return Color("#ebdfd3")
	else: return Color("#2c2014")

func _init() -> void: size = Vector2(18,18)

func _ready() -> void:
	add_to_group(&"hasNumbers", true)
	drawScaled = RenderingServer.canvas_item_create()
	drawAuraBreaker = RenderingServer.canvas_item_create()
	drawGlitch = RenderingServer.canvas_item_create()
	drawError = RenderingServer.canvas_item_create()
	drawMain = RenderingServer.canvas_item_create()
	drawConfiguration = RenderingServer.canvas_item_create()
	RenderingServer.canvas_item_set_parent(drawScaled,get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawAuraBreaker,get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawGlitch,get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawMain,get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawError,get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawConfiguration,get_canvas_item())
	textDrawer = TextDrawer.new(self, TextDrawer.SETTING.FTALK)
	RenderingServer.canvas_item_set_self_modulate(drawError, "#ffffffaa")
	RenderingServer.canvas_item_set_material(drawError,Game.ADDITIVE_MATERIAL)
	Game.connect(&"goldIndexChanged",func(): if animated(): queue_redraw())

func animated() -> bool:
	if Colors.getDef(getColor(COLOR_STEP.DRAW_BASE)).doorTextureFrames > 1: return true
	if getColor(COLOR_STEP.BASE) == C.olors.ERROR: return true
	if armament: return true
	return false

func _freed() -> void:
	RenderingServer.free_rid(drawScaled)
	RenderingServer.free_rid(drawAuraBreaker)
	RenderingServer.free_rid(drawGlitch)
	RenderingServer.free_rid(drawMain)
	RenderingServer.free_rid(drawError)
	RenderingServer.free_rid(drawConfiguration)

func convertNumbers(from:M.SYSTEM) -> void:
	Changes.addChange(Changes.ComponentConvertNumberChange.new(self, from, &"count"))
	Changes.addChange(Changes.ComponentConvertNumberChange.new(self, from, &"denominator"))

func _draw() -> void:
	RenderingServer.canvas_item_clear(drawScaled)
	RenderingServer.canvas_item_clear(drawAuraBreaker)
	RenderingServer.canvas_item_clear(drawGlitch)
	RenderingServer.canvas_item_clear(drawMain)
	RenderingServer.canvas_item_clear(drawError)
	RenderingServer.canvas_item_clear(drawConfiguration)
	if !parent.active and Game.playState == Game.PLAY_STATE.PLAY:
		textDrawer.evaluate()
		return
	drawLock(drawScaled,drawAuraBreaker,drawGlitch,drawMain,drawConfiguration,textDrawer,
		size,getColor(COLOR_STEP.DRAW_BASE),getColor(COLOR_STEP.Glitch),type,effectiveConfiguration(),sizeType,effectiveCount(),effectiveZeroI(),isPartial,effectiveDenominator(),partialBlastHorizontal,negated,armament,
		getFrameHighColor(isNegative(), negated),
		getFrameMainColor(isNegative(), negated),
		getFrameDarkColor(isNegative(), negated),
		isNegative(),
		parent.animState != Door.ANIM_STATE.RELOCK or parent.animPart > 2,
		Game.playState == Game.PLAY_STATE.PLAY and parent.drawComplex,
		spendType
	)
	if getColor(COLOR_STEP.BASE) == C.olors.ERROR:
		RenderingServer.canvas_item_add_texture_rect(drawError,Rect2(-offsetFromType(sizeType), size),ERROR_FX.current([randi_range(0,2)]))

static func drawLock(lockDrawScaled:RID, lockDrawAuraBreaker:RID, lockDrawGlitch:RID, lockDrawMain:RID, lockDrawConfiguration:RID, lockTextDrawer:TextDrawer,
	lockSize:Vector2,
	lockBaseColor:C.olors, lockGlitchColor:C.olors,
	lockType:TYPE,
	lockConfiguration:CONFIGURATION,
	lockSizeType:SIZE_TYPE,
	lockCount:PackedInt64Array,
	lockZeroI:bool,
	lockIsPartial:bool,
	lockDenominator:PackedInt64Array,
	lockPartialBlastHorizontal:bool,
	lockNegated:bool,
	lockArmament:bool,
	frameHigh:Color,frameMain:Color,frameDark:Color,
	negative:bool, drawFill:bool=true, noCopies:bool=false,
	spendTypes:SPEND_TYPE=SPEND_TYPE.NORMAL
) -> void:
	var rect:Rect2 = Rect2(-offsetFromType(lockSizeType), lockSize)
	if lockNegated:
		RenderingServer.canvas_item_set_transform(lockDrawScaled,Transform2D(PI,lockSize-offsetFromType(lockSizeType)*2))
		RenderingServer.canvas_item_set_transform(lockDrawConfiguration,Transform2D(PI,lockSize-offsetFromType(lockSizeType)*2))
	else:
		RenderingServer.canvas_item_set_transform(lockDrawScaled,Transform2D.IDENTITY)
		RenderingServer.canvas_item_set_transform(lockDrawConfiguration,Transform2D.IDENTITY)
	# fill
	if drawFill:
		if Colors.getDef(lockBaseColor).doorTexture:
			var tileTexture:bool = Colors.getDef(lockBaseColor).doorTextureTile
			RenderingServer.canvas_item_add_texture_rect(lockDrawScaled,rect,Game.COLOR_TEXTURES.current([lockBaseColor]),tileTexture)
		elif lockBaseColor == C.olors.GLITCH:
			RenderingServer.canvas_item_set_material(lockDrawGlitch,Game.GLITCH_MATERIAL.get_rid())
			RenderingServer.canvas_item_add_rect(lockDrawGlitch,Rect2(rect.position+Vector2.ONE,rect.size-Vector2(2,2)),Colors.getMainTone(lockBaseColor))
			if lockGlitchColor != C.olors.GLITCH:
				if lockSizeType == SIZE_TYPE.ANY:
					if Colors.getDef(lockGlitchColor).doorTexture: RenderingServer.canvas_item_add_nine_patch(lockDrawMain,rect,GLITCH_ANY_RECT,GLITCH_FILL_TEXTURE.current([lockGlitchColor,lockSizeType]),GLITCH_CORNER_SIZE,GLITCH_CORNER_SIZE,TILE,TILE)
					else: RenderingServer.canvas_item_add_nine_patch(lockDrawMain,rect,GLITCH_ANY_RECT,GLITCH_FILL.current([lockSizeType]),GLITCH_CORNER_SIZE,GLITCH_CORNER_SIZE,TILE,TILE,true,Colors.getMainTone(lockGlitchColor))
				elif Colors.getDef(lockGlitchColor).doorTexture: RenderingServer.canvas_item_add_texture_rect(lockDrawMain,rect,GLITCH_FILL_TEXTURE.current([lockGlitchColor,lockSizeType]))
				else: RenderingServer.canvas_item_add_texture_rect(lockDrawMain,rect,GLITCH_FILL.current([lockSizeType]),false,Colors.getMainTone(lockGlitchColor))
		elif lockBaseColor in [C.olors.ICE, C.olors.MUD, C.olors.GRAFFITI]:
			RenderingServer.canvas_item_set_material(lockDrawScaled,Game.NO_MATERIAL.get_rid())
			RenderingServer.canvas_item_add_rect(lockDrawScaled,Rect2(rect.position+Vector2.ONE,rect.size-Vector2(2,2)),Colors.getMainTone(lockBaseColor))
			Door.drawAuras(lockDrawAuraBreaker,lockDrawAuraBreaker,lockDrawAuraBreaker,lockBaseColor==C.olors.ICE,lockBaseColor==C.olors.MUD,lockBaseColor==C.olors.GRAFFITI,rect)
		else:
			RenderingServer.canvas_item_add_rect(lockDrawMain,Rect2(rect.position+Vector2.ONE,rect.size-Vector2(2,2)),Colors.getMainTone(lockBaseColor))
	# background (spend types)
	var bgColor:Color
	# lighter if negative, darker if positive
	if negative: bgColor = Color("#ffffff66")
	else: bgColor = Color("#00000066")
	if spendTypes == SPEND_TYPE.STAR: RenderingServer.canvas_item_add_texture_rect(lockDrawMain, rect, BACKGROUND_STARRY, true, bgColor)
	if spendTypes == SPEND_TYPE.NONE: RenderingServer.canvas_item_add_texture_rect(lockDrawMain, rect, BACKGROUND_WEAK, true, bgColor)
	if spendTypes == SPEND_TYPE.ALL: RenderingServer.canvas_item_add_texture_rect(lockDrawMain, rect, BACKGROUND_FORCEFUL, true, bgColor)
	if noCopies:
		lockTextDrawer.evaluate()
		return # no copies in this direction; go away
	# frame
	RenderingServer.canvas_item_add_nine_patch(lockDrawMain,rect,ANY_RECT,FRAME_HIGH,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,frameHigh)
	RenderingServer.canvas_item_add_nine_patch(lockDrawMain,rect,ANY_RECT,FRAME_MAIN,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,frameMain)
	RenderingServer.canvas_item_add_nine_patch(lockDrawMain,rect,ANY_RECT,FRAME_DARK,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,frameDark)
	if lockArmament: RenderingServer.canvas_item_add_nine_patch(lockDrawMain,rect,ARMAMENT_RECT,ARMAMENT[Game.goldIndex%4],ARMAMENT_CORNER_SIZE,ARMAMENT_CORNER_SIZE,TILE,TILE,false)
	# configuration
	var center:Vector2 = rect.position+round(lockSize/2)
	if lockConfiguration == CONFIGURATION.NONE:
		match lockType:
			TYPE.NORMAL,TYPE.EXACT,TYPE.GLISTENING,TYPE.REMAINDER:
				var drawColor:Color = getConfigurationColor(negative)
				var noNumber:bool = M.eq(lockCount, M.ONE())
				var noSymbol:bool = lockType == TYPE.NORMAL and (M.isNonzeroImag(lockCount) or (lockSize == Vector2(18,18) and !noNumber))
				var vertical:bool = lockSize.x == 18 and lockSize.y != 18 and !noNumber
				var symbolLast:bool = false
				var symbol:Texture2D
				var effectiveSymbolSize:Vector2 = Vector2.ZERO
				if !noSymbol:
					effectiveSymbolSize.y = 12
					match lockType:
							TYPE.NORMAL:
								symbol = SYMBOL_NORMAL
								effectiveSymbolSize.x = 14
							TYPE.EXACT:
								if M.isNonzeroImag(lockCount) or lockZeroI:
									symbol = SYMBOL_EXACTI
									effectiveSymbolSize.x = 6
									symbolLast = true
								else:
									symbol = SYMBOL_EXACT
									effectiveSymbolSize.x = 12
							TYPE.GLISTENING:
								if M.isNonzeroImag(lockCount) or lockZeroI:
									symbol = SYMBOL_GLISTENINGI
									effectiveSymbolSize.x = 8
									symbolLast = true
								else:
									symbol = SYMBOL_GLISTENING
									effectiveSymbolSize.x = 12
							TYPE.REMAINDER:
								symbol = SYMBOL_REMAINDER
								effectiveSymbolSize.x = 14
				lockTextDrawer.addVerticalContext(center+Vector2(0,2) if vertical and M.isInteger(lockCount) else center, TextDrawer.VERTICAL_ALIGN.CENTER)
				lockTextDrawer.addHorizontalContext(Vector2.ZERO, TextDrawer.HORIZONTAL_ALIGN.CENTER)
				lockTextDrawer.addSpacing(2)
				if vertical:
					var gap:float = 7 if M.isInteger(lockCount) else 2
					if !noNumber:
						lockTextDrawer.addNumber(M.abs(lockCount), drawColor)
					if lockType == TYPE.NORMAL and M.isNonzeroImag(lockCount):
						lockTextDrawer.addNewline(gap,TextDrawer.HORIZONTAL_ALIGN.CENTER)
						lockTextDrawer.addString("i", drawColor)
					elif !noSymbol:
						lockTextDrawer.addNewline(gap,TextDrawer.HORIZONTAL_ALIGN.CENTER)
						lockTextDrawer.addImage(symbol, lockNegated, drawColor, effectiveSymbolSize)
				else:
					if symbolLast and !noNumber: lockTextDrawer.addNumber(M.abs(lockCount), drawColor)
					if !noSymbol: lockTextDrawer.addImage(symbol, lockNegated, drawColor, effectiveSymbolSize)
					if !symbolLast and !noNumber: lockTextDrawer.addNumber(M.abs(lockCount), drawColor)
			TYPE.BLANK: pass # nothing really
			TYPE.BLAST, TYPE.ALL:
				var drawColor:Color = getConfigurationColor(negative)
				var numer:PackedInt64Array
				var denom:PackedInt64Array
				var ipow:int = 0
				if M.isComplex(lockDenominator) or M.nex(lockDenominator) or M.isComplex(lockCount) or M.isError(lockDenominator):
					numer = lockCount
					denom = lockDenominator
				else:
					numer = M.divide(lockCount, M.axis(lockDenominator))
					denom = M.abs(lockDenominator)
					ipow = M.toIpow(M.axis(lockDenominator))
				var noNumeratorNumber:bool = M.eq(numer, M.ONE())
				var symbol:Texture2D
				match ipow:
					0, 2: symbol = SYMBOL_BLAST
					1, 3: symbol = SYMBOL_BLASTI
				if lockType == TYPE.ALL: symbol = SYMBOL_ALL

				lockTextDrawer.addVerticalContext(center, TextDrawer.VERTICAL_ALIGN.CENTER)
				lockTextDrawer.addHorizontalContext(Vector2.ZERO, TextDrawer.HORIZONTAL_ALIGN.CENTER)
				if lockPartialBlastHorizontal:
					if !noNumeratorNumber: lockTextDrawer.addNumber(numer, drawColor)
					lockTextDrawer.addImage(symbol, lockNegated, drawColor, Vector2(10,12))
					lockTextDrawer.addImage(PARTIAL_BLAST_HORIZONTAL if M.isInteger(numer) and M.isInteger(denom) else PARTIAL_BLAST_HORIZONTAL_BIG, lockNegated, drawColor, Vector2(8,12))
					lockTextDrawer.addNumber(denom, drawColor)
				else:
					if !noNumeratorNumber: lockTextDrawer.addNumber(numer, drawColor)
					lockTextDrawer.addImage(symbol, lockNegated, drawColor, Vector2(10,12))

					if lockIsPartial:
						var numeratorWidth:float = 10
						if !noNumeratorNumber: numeratorWidth += lockTextDrawer.getNumberWidth(numer)
						var denominatorWidth:float = lockTextDrawer.getNumberWidth(denom)
						var maxWidth:float = max(numeratorWidth, denominatorWidth)
						lockTextDrawer.addNewline(3, TextDrawer.HORIZONTAL_ALIGN.CENTER)
						lockTextDrawer.addCustom(drawPartialBlastVerticalLine, [maxWidth, drawColor], 0, 0, 2)
						lockTextDrawer.addNewline(3, TextDrawer.HORIZONTAL_ALIGN.CENTER)
						lockTextDrawer.addSpacing(2)
						lockTextDrawer.addNumber(denom, drawColor)
				
	else: RenderingServer.canvas_item_add_texture_rect(lockDrawConfiguration,rect,getPredefinedLockSprite(lockCount,lockType,lockConfiguration),false,getConfigurationColor(negative))
	lockTextDrawer.evaluate()

static func drawPartialBlastVerticalLine(lockDrawMain:RID, drawPosition:Vector2, params:Array) -> void:
	var maxWidth:float = params[0]
	var drawColor:Color = params[1]
	RenderingServer.canvas_item_add_rect(lockDrawMain, Rect2(drawPosition-Vector2(round(maxWidth/2), 0), Vector2(maxWidth, 2)), drawColor)

func getDrawPosition() -> Vector2: return position + parent.position - getOffset()

func _simpleDoorUpdate() -> void:
	# resize and set configuration	
	var newSizeType:SIZE_TYPE
	match parent.size:
		Vector2(32,32): newSizeType = SIZE_TYPE.AnyS
		Vector2(64,32): newSizeType = SIZE_TYPE.AnyH
		Vector2(32,64): newSizeType = SIZE_TYPE.AnyV
		Vector2(64,64): newSizeType = SIZE_TYPE.AnyL
		Vector2(96,96): newSizeType = SIZE_TYPE.AnyXL
		_: newSizeType = SIZE_TYPE.ANY
	Changes.addChange(Changes.PropertyChange.new(self,&"position",Vector2.ZERO))
	Changes.addChange(Changes.PropertyChange.new(self,&"sizeType",newSizeType))
	Changes.addChange(Changes.PropertyChange.new(self,&"size",parent.size - Vector2(14,14)))
	queue_redraw()

func _comboDoorConfigurationChanged(newSizeType:SIZE_TYPE,newConfiguration:CONFIGURATION=CONFIGURATION.NONE) -> void:
	Changes.addChange(Changes.PropertyChange.new(self,&"sizeType",newSizeType))
	Changes.addChange(Changes.PropertyChange.new(self,&"configuration",newConfiguration))
	var newSize:Vector2
	match sizeType:
		SIZE_TYPE.AnyS: newSize = Vector2(18,18)
		SIZE_TYPE.AnyH: newSize = Vector2(50,18)
		SIZE_TYPE.AnyV: newSize = Vector2(18,50)
		SIZE_TYPE.AnyM: newSize = Vector2(38,38)
		SIZE_TYPE.AnyL: newSize = Vector2(50,50)
		SIZE_TYPE.AnyXL: newSize = Vector2(82,82)
	if newSize: Changes.addChange(Changes.PropertyChange.new(self,&"size",newSize))
	queue_redraw()

func _comboDoorSizeChanged() -> void:
	var newSizeType:SIZE_TYPE = SIZE_TYPE.ANY
	match size:
		Vector2(18,18): newSizeType = SIZE_TYPE.AnyS
		Vector2(50,18): newSizeType = SIZE_TYPE.AnyH
		Vector2(18,50): newSizeType = SIZE_TYPE.AnyV
		Vector2(38,38): newSizeType = SIZE_TYPE.AnyM
		Vector2(50,50): newSizeType = SIZE_TYPE.AnyL
		Vector2(82,82): newSizeType = SIZE_TYPE.AnyXL
	Changes.addChange(Changes.PropertyChange.new(self,&"sizeType",newSizeType))
	if [sizeType, configuration] not in getAvailableConfigurations():
		Changes.addChange(Changes.PropertyChange.new(self,&"configuration",CONFIGURATION.NONE))

static func getAutoConfiguration(lock:GameComponent) -> CONFIGURATION:
	var newConfiguration:CONFIGURATION = CONFIGURATION.NONE
	for option in lock.getAvailableConfigurations():
		if lock.sizeType == option[0]:
			newConfiguration = option[1]
			break
	return newConfiguration

func _setAutoConfiguration() -> void:
	Changes.addChange(Changes.PropertyChange.new(self,&"configuration",getAutoConfiguration(self)))

func receiveMouseInput(event:InputEventMouse) -> bool:
	# resizing
	if !editor.edgeResizing or editor.componentDragged: return false
	if !Rect2(position-getOffset(),size).has_point(editor.mouseWorldPosition - parent.position) : return false
	var dragCornerSize:Vector2 = Vector2(8,8)/editor.cameraZoom
	var diffSign:Vector2 = Editor.rectSign(Rect2(position+dragCornerSize-getOffset(),size-dragCornerSize*2), editor.mouseWorldPosition-parent.position)
	if !diffSign: return false
	elif !diffSign.x: editor.mouse_default_cursor_shape = Control.CURSOR_VSIZE
	elif !diffSign.y: editor.mouse_default_cursor_shape = Control.CURSOR_HSIZE
	elif (diffSign.x > 0) == (diffSign.y > 0): editor.mouse_default_cursor_shape = Control.CURSOR_FDIAGSIZE
	else: editor.mouse_default_cursor_shape = Control.CURSOR_BDIAGSIZE
	if Editor.isLeftClick(event):
		editor.startSizeDrag(self, diffSign)
		return true
	return false

func _coerceSize() -> void:
	var newSize = (size+Vector2(14,14)).snapped(Vector2(16,16))
	if newSize == Vector2(48,48):
		newSize = Vector2(38,38)
	else:
		newSize = (size+Vector2(14,14)).snapped(Vector2(32,32)) - Vector2(14,14)
		if newSize in SIZES: return
		newSize = newSize.min(Vector2(82,82))
		# 1x3, 2x3 -> 3x3
		if newSize.x < newSize.y: newSize = Vector2(newSize.y, newSize.y)
		elif newSize.y < newSize.x: newSize = Vector2(newSize.x, newSize.x)
	Changes.addChange(Changes.PropertyChange.new(self,&"size",newSize))

func propertyChangedInit(property:StringName) -> void:
	if parent.type != Door.TYPE.SIMPLE:
		if property == &"size": _comboDoorSizeChanged()
	if property in [&"count", &"sizeType", &"type"]: _setAutoConfiguration()
	lockPropertyChangedInit(self, property)
	if property in [&"color", &"type"] and editor.focusDialog.focused == parent: editor.focusDialog.doorDialog.lockHandler.redrawButton(index)
	if property == &"armament" and armament:
		parent._allowAurasCheck()

static func lockPropertyChangedInit(lock:GameComponent, property:StringName) -> void:
	if property == &"type":
		if (lock.type == TYPE.BLANK or (lock.type == TYPE.ALL and !Mods.active(&"PartialBlastLocks"))) and M.neq(lock.count, M.ONE()):
			Changes.addChange(Changes.PropertyChange.new(lock,&"count",M.ONE()))
		if lock.type != TYPE.EXACT and lock.zeroI:
			Changes.addChange(Changes.PropertyChange.new(lock,&"zeroI",false))
		if lock.type == TYPE.BLAST:
			if !Mods.active(&"PartialBlastLocks"):
				if M.neq(M.abs(lock.count), M.ONE()): Changes.addChange(Changes.PropertyChange.new(lock,&"count",M.saxis(lock.count)))
				if M.neq(M.axis(lock.denominator), M.axis(lock.count)): Changes.addChange(Changes.PropertyChange.new(lock,&"denominator", M.axis(lock.count)))
		elif lock.type == TYPE.ALL:
			if !lock.isPartial and M.neq(lock.denominator, M.ONE()): Changes.addChange(Changes.PropertyChange.new(lock,&"denominator",M.ONE()))
		else:
			if M.neq(lock.denominator, M.ONE()): Changes.addChange(Changes.PropertyChange.new(lock,&"denominator",M.ONE()))
			if lock.isPartial: Changes.addChange(Changes.PropertyChange.new(lock,&"isPartial",false))
			if M.isComplex(lock.count):
				Changes.addChange(Changes.PropertyChange.new(lock,&"count",M.r(lock.count)))

	if property == &"isPartial" and !lock.isPartial:
		Changes.addChange(Changes.PropertyChange.new(lock,&"denominator", M.ONE() if M.isComplex(lock.count) or M.nex(lock.count) or lock.type == TYPE.ALL else M.axis(lock.count)))

func propertyChangedDo(property:StringName) -> void:
	if property in [&"count", &"denominator"] and parent: parent.queue_redraw()
	if property == &"armament" and parent: parent.reindexLocks()
	if property in [&"size", &"position"] and parent and parent.armament: parent.queue_redraw()

# ==== PLAY ==== #
var glitchMimic:C.olors = C.olors.GLITCH
var errorMimic:C.olors = C.olors.ERROR
func stop() -> void:
	glitchMimic = C.olors.GLITCH
	errorMimic = C.olors.ERROR

enum COLOR_STEP {INITIAL, Curse, BASE, Error, DRAW_BASE, Glitch, EFFECTIVE, AuraBreaker, FINAL}

func getColor(step:COLOR_STEP) -> C.olors:
	var resultColor:C.olors = color

	if step < COLOR_STEP.Curse: return resultColor
	var curseAffected:bool = parent.cursed and parent.curseColor != C.olors.PURE and !armament
	if curseAffected: resultColor = parent.curseColor
	
	# BASE
	# redundancy checks go here, like cant freeze if all ice

	if step < COLOR_STEP.Error: return resultColor
	var checkColor:C.olors = resultColor # error and glitch act independently
	if checkColor == C.olors.ERROR: resultColor = parent.curseMimic if curseAffected else errorMimic

	# DRAW_BASE
	# the step used for drawing

	if step < COLOR_STEP.Glitch: return resultColor
	if checkColor == C.olors.GLITCH: resultColor = parent.curseMimic if curseAffected else glitchMimic

	# EFFECTIVE
	# the step used for normal immunities

	if step < COLOR_STEP.AuraBreaker: return resultColor
	if !armament:
		if parent.gameFrozen: resultColor = C.olors.ICE
		if parent.gameCrumbled: resultColor = C.olors.MUD
		if parent.gamePainted: resultColor = C.olors.GRAFFITI

	# FINAL
	# the step used for check and cost
	return resultColor

func effectiveConfiguration() -> CONFIGURATION:
	if Game.simpleLocks: return CONFIGURATION.NONE
	if M.neq(parent.ipow(), M.ONE()):
		if parent.type == Door.TYPE.SIMPLE: return getAutoConfiguration(self)
		else: return CONFIGURATION.NONE
	else: return configuration

func canOpen(player:Player, checkColor:C.olors=getColor(COLOR_STEP.FINAL)) -> bool: return getLockCanOpen(self, player, checkColor)

static func getLockCanOpen(lock:GameComponent, player:Player, checkColor:C.olors=lock.getColor(COLOR_STEP.FINAL)) -> bool:
	if checkColor == C.olors.FIRE: return !lock.negated
	var can:bool = true
	var keyCount:PackedInt64Array = player.key[checkColor]
	var glistenCount:PackedInt64Array = player.glisten[checkColor]
	var lockCount:PackedInt64Array = lock.effectiveCount()
	var lockDenominator:PackedInt64Array = lock.effectiveDenominator()
	if M.isError(keyCount): return lock.negated
	match lock.type:
		TYPE.NORMAL: can = M.cgte(M.along(keyCount, lockCount), M.cabs(lockCount))
		TYPE.BLANK: can = M.nex(keyCount)
		TYPE.BLAST:
			if M.nex(lockDenominator): can = false
			elif !M.simplies(lockDenominator, keyCount): can = false
			elif lock.isPartial:
				if !M.divisibleBy(M.alongbs(keyCount, lockDenominator), lockDenominator): can = false
				elif M.neq(M.sign(M.divide(M.alongbs(keyCount, lockDenominator), lockDenominator)), M.ONE()): can = false
		TYPE.ALL:
			if M.nex(lockDenominator): can = false
			elif M.nex(keyCount): can = false
			elif lock.isPartial:
				if !M.divisibleBy(keyCount, lockDenominator): can = false
		TYPE.EXACT:
			if M.nex(lockCount):
				if lock.effectiveZeroI(): can = M.nex(M.i(keyCount))
				else: can = M.nex(M.r(keyCount))
			else: can = M.eq(M.along(keyCount, lockCount), M.cabs(lockCount))
		TYPE.GLISTENING: can = M.cgte(M.along(glistenCount, lockCount), M.cabs(lockCount))
		TYPE.REMAINDER: can = M.neq(M.partialRemainder(keyCount, lockCount),M.ZERO())
	return can != lock.negated

func getCost(player:Player, airEffect:bool, ipow:PackedInt64Array=parent.ipow()) -> PackedInt64Array: return getLockCost(self, airEffect, player, ipow)

static func getLockCost(lock:GameComponent, airEffect:bool, player:Player, ipow:PackedInt64Array) -> PackedInt64Array:
	var checkColor:C.olors
	if airEffect and M.ex(player.key[C.olors.AIR]) and !lock.canOpen(player): checkColor = C.olors.AIR
	else: checkColor = lock.getColor(COLOR_STEP.FINAL)
	var cost:PackedInt64Array = M.ZERO()
	var keyCount:PackedInt64Array = player.key[checkColor]
	var lockCount:PackedInt64Array = lock.effectiveCount(ipow)
	var lockDenominator:PackedInt64Array = lock.effectiveDenominator(ipow)
	if lock.getColor(Lock.COLOR_STEP.FINAL) == C.olors.EARTH and lock.type != Lock.TYPE.BLAST: cost = player.key[C.olors.EARTH]
	else:
		match lock.type:
			TYPE.NORMAL, TYPE.EXACT, TYPE.GLISTENING: cost = lockCount
			TYPE.BLAST: if M.ex(lockDenominator): cost = M.divide(M.times(M.alongbs(keyCount, lockDenominator), lockCount), lockDenominator)
			TYPE.REMAINDER: cost = M.partialRemainder(keyCount, lockCount)
			TYPE.ALL: if M.ex(lockDenominator): cost = M.divide(M.times(keyCount, lockCount), lockDenominator)
	if lock.negated: return M.negate(cost)
	return cost

func effectiveCount(ipow:PackedInt64Array=parent.ipow()) -> PackedInt64Array:
	return M.times(count, ipow)

func effectiveDenominator(ipow:PackedInt64Array=parent.ipow()) -> PackedInt64Array:
	return M.times(denominator, ipow)

func effectiveZeroI() -> bool: return (zeroI != M.isNonzeroImag(parent.ipow())) and M.nex(count)

func isNegative() -> bool:
	if type in [TYPE.BLAST, TYPE.ALL]:
		if M.isComplex(count) or M.isComplex(denominator): return false
		return M.negative(M.sign(effectiveDenominator()))
	return M.negative(M.sign(effectiveCount()))
